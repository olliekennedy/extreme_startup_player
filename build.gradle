plugins {
    id 'java'
    id 'application'
    id 'jacoco'
    id 'info.solidsoft.pitest' version '1.15.0'
    id 'org.jetbrains.kotlin.jvm'
}

version = '1.0.0'

description = """extreme_startup_player"""

mainClassName = 'com.oocode.ExtremeStartupHttpServer'

task fatJar(type: Jar) {
    manifest {
        attributes "Main-Class": mainClassName
    }
    archiveBaseName = 'extreme_startup_player-fat-jar'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'io.fusionauth:java-http:0.2.10'

    testImplementation(platform('org.junit:junit-bom:5.10.1'))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation 'com.oneeyedmen:okeydoke:2.0.3'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}
jacoco {
    toolVersion = "0.8.11"
}
jacocoTestReport {
    reports {
        html.outputLocation = file("${buildDir}/jacocoHtml")
    }
}
pitest{
    junit5PluginVersion = '1.2.1'
    pitestVersion = '1.15.2'
    targetClasses=["com.oocode.*"]
}
